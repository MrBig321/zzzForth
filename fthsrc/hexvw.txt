( Uses dump.txt)
: HEXVIEW ;  ( marker for FORGET)

DECIMAL 

: KEY? KEYW DISCARD DROP ; 

360 CONSTANT BYTESPERSCR

28 CONSTANT KEYPGUP
29 CONSTANT KEYPGDN
22 CONSTANT KEYUP
23 CONSTANT KEYDOWN
27 CONSTANT KEYESC

31 CONSTANT NHROWS 
64 CONSTANT NHCOLS 

VARIABLE MCURX 	( Saved cursor-position of the main-screen) ( 2VARIABLE !?)
VARIABLE MCURY

: SAVEMCUR  ( -- )
   GET-XY MCURY ! MCURX ! ;

: RESTOREMCUR  ( -- )
   MCURX @ MCURY @ AT-XY ;

: INITHCURSOR  ( -- )
   11 0 AT-XY ;

: DOMSCR>  ( -- )
   0 0 NHCOLS NHROWS MSCR> ;

: DO>MSCR  ( -- )
   >MSCR OSPARSON SCROLLON RESTOREMCUR ;

: SHOWCTRLTEXT  ( -- )
    3 NHROWS AT-XY ." PGUP PGDN " 
   14 NHROWS AT-XY 127 EMIT  
   16 NHROWS AT-XY 128 EMIT  
   43 NHROWS AT-XY ." Press ESC to quit" ;

: INITSCR  ( -- )
   OSPARSOFF SCROLLOFF 
   32 REMCUR SAVEMCUR PAGE  
   SHOWCTRLTEXT 
   DOMSCR> INITHCURSOR ;

: PRINTABLE? ( c -- flag )
    DUP 32 >= SWAP 127 < AND ;

VARIABLE BEGADDR

: KPGUP  ( -- )
   PAGE BEGADDR @ BYTESPERSCR - DUP BEGADDR ! BYTESPERSCR HDUMP ;

: KPGDN  ( -- )
   PAGE BEGADDR @ BYTESPERSCR + DUP BEGADDR ! BYTESPERSCR HDUMP ;

: KUP  ( -- )
   PAGE BEGADDR @ BYTESPERROW - DUP BEGADDR ! BYTESPERSCR HDUMP ;

: KDOWN  ( -- )
   PAGE BEGADDR @ BYTESPERROW + DUP BEGADDR ! BYTESPERSCR HDUMP ;

: CTRLKEYS  ( key -- )
    CASE 
      KEYPGUP OF KPGUP ENDOF 
      KEYPGDN OF KPGDN ENDOF 
      KEYUP   OF KUP   ENDOF 
      KEYDOWN OF KDOWN ENDOF 
    ENDCASE ;

: KEYS  ( -- )
   BEGIN 
     KEYW DISCARD 
     DUP CTRLKEYS
     KEYESC = 
   UNTIL ;

: HEXVW  ( addr -- )
   HEX DUP BEGADDR ! 
   INITSCR BYTESPERSCR HDUMP KEYS DO>MSCR ; 


